allprojects {
    group 'dev.johanness'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    pluginManager.withPlugin('java') {
        java {
            sourceCompatibility = JavaVersion.VERSION_11
            withSourcesJar()
        }
        test {
            useJUnitPlatform()
        }
        task testJava11(type: Test) {
            description = 'Runs the test suite on Java 11.'
            group = 'verification'
            useJUnitPlatform()
            shouldRunAfter(test)
            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(11)
            }
        }
        check {
            dependsOn testJava11
        }
        dependencies {
            constraints {
                implementation 'com.google.testing.compile:compile-testing:0.19'
                implementation 'org.jetbrains:annotations:23.0.0'
                implementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
                implementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
                implementation 'org.mockito:mockito-inline:4.3.1'
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }
}
